apply plugin: 'com.android.application'

android {
    compileSdkVersion 23
    buildToolsVersion '23.0.1'
    defaultConfig {
        applicationId "th.in.whs.ku.bus"
        minSdkVersion 9
        targetSdkVersion 22
        versionCode 25
        versionName "1.12.2"
    }
    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-project.txt'
        }
    }
    productFlavors {
    }

    sourceSets {
        main {
            java {
                srcDir 'src/main/gen'
            }
        }
    }
}

configurations {
    protobuf
}

dependencies {
    compile 'com.koushikdutta.async:androidasync:2.+'
    compile project(':appRater')
    compile project(':sentry')
    compile 'com.android.support:appcompat-v7:22.+'
    compile 'com.google.android.gms:play-services-location:8.1.+'
    compile 'com.google.android.gms:play-services-maps:8.1.+'
    compile 'com.google.android.gms:play-services-gcm:8.1.+'
    compile 'com.google.android.gms:play-services-analytics:8.1.+'
    compile 'com.squareup.okhttp:okhttp:2.+'
    compile 'com.squareup.wire:wire-runtime:1.+'
    protobuf 'com.squareup.wire:wire-compiler:1.+'
}

task compileProtobuf(type: JavaExec) {
    classpath configurations.protobuf.asPath
    main 'com.squareup.wire.WireCompiler'

    def java_args = ['--proto_path=protobuf', '--java_out=src/main/gen/']
    java_args += file('protobuf/').listFiles().findAll {it.name.endsWith('.proto')}.collect{it.name}
    args java_args
}

tasks.preBuild.dependsOn compileProtobuf